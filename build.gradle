buildscript {
    ext.kotlin_version = '2.0.20'
    if (!project.hasProperty('version') || project.version.equals('unspecified')) {
        project.version = '+'
    }

    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:9.0.1'
        classpath 'com.mparticle:android-kit-plugin:' + project.version
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.sonarqube" version "3.5.0.2730"
    id "org.jlleitschuh.gradle.ktlint" version "13.0.0"
    id "org.jetbrains.kotlin.plugin.compose" version "2.0.20"
}

sonarqube {
    properties {
        property "sonar.projectKey", "mparticle-android-integration-button"
        property "sonar.organization", "mparticle"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'kotlin-android'
apply plugin: 'com.mparticle.kit'

android {
    namespace 'com.mparticle.kits.rokt'
    defaultConfig {
        minSdkVersion 21
        consumerProguardFiles 'consumer-proguard.pro'
    }
    lint {
        disable "NullSafeMutableLiveData"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            jvmArgs += ['--add-opens', 'java.base/java.lang=ALL-UNNAMED']
            jvmArgs += ['--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED']
        }
    }
    buildFeatures {
        buildConfig = true
        compose true
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    def composeBom = platform('androidx.compose:compose-bom:2023.10.01')
    implementation composeBom
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'androidx.compose.runtime:runtime'
    api 'com.rokt:roktsdk:4.14.0'

    testImplementation  files('libs/java-json.jar')
    testImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    testImplementation ("io.mockk:mockk:1.13.4")
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    compileOnly 'androidx.compose.ui:ui'
    compileOnly 'androidx.compose.material:material'
    compileOnly 'androidx.compose.ui:ui-tooling'
}

ktlint {
    android = true
    reporters {
        reporter "html"
    }
}
